/*!
 * \file fastuidraw_painter_util.geom.glsl.resource_string
 * \brief file fastuidraw_painter_util.geom.glsl.resource_string
 *
 * Copyright 2018 by Intel.
 *
 * Contact: kevin.rogovin@intel.com
 *
 * This Source Code Form is subject to the
 * terms of the Mozilla Public License, v. 2.0.
 * If a copy of the MPL was not distributed with
 * this file, You can obtain one at
 * http://mozilla.org/MPL/2.0/.
 *
 * \author Kevin Rogovin <kevin.rogovin@intel.com>
 *
 */
layout(triangles) in;

#ifdef FASTUIDRAW_PAINTER_GEOMETRY_SHADER_CLIPPING

layout(triangle_strip, max_vertices = 7) out;

struct fastuidraw_clipper_data_field
{
  uvec4 element0_3;
  uvec4 element4_7;
};

struct fastuidraw_clipper_data
{
  int count; // 0 <= count <= 7
  fastuidraw_clipper_data_field b0, b1, b2;
  vec3 last_barycentric;
};

#define fastuidraw_clipper_data_bary0(A) uintBitsToFloat(uvec3(A.b0.element0_3.x, A.b1.element0_3.x, A.b2.element0_3.x))
#define fastuidraw_clipper_data_bary1(A) uintBitsToFloat(uvec3(A.b0.element0_3.y, A.b1.element0_3.y, A.b2.element0_3.y))
#define fastuidraw_clipper_data_bary2(A) uintBitsToFloat(uvec3(A.b0.element0_3.z, A.b1.element0_3.z, A.b2.element0_3.z))
#define fastuidraw_clipper_data_bary3(A) uintBitsToFloat(uvec3(A.b0.element0_3.w, A.b1.element0_3.w, A.b2.element0_3.w))
#define fastuidraw_clipper_data_bary4(A) uintBitsToFloat(uvec3(A.b0.element4_7.x, A.b1.element4_7.x, A.b2.element4_7.x))
#define fastuidraw_clipper_data_bary5(A) uintBitsToFloat(uvec3(A.b0.element4_7.y, A.b1.element4_7.y, A.b2.element4_7.y))
#define fastuidraw_clipper_data_bary6(A) uintBitsToFloat(uvec3(A.b0.element4_7.z, A.b1.element4_7.z, A.b2.element4_7.z))

struct fastuidraw_clipper_data_iterator
{
  int count;
};

void
fastuidraw_clipper_data_iterator_init_front(out fastuidraw_clipper_data_iterator d,
                                            in fastuidraw_clipper_data src)
{
  d.count = 0;
}


vec3
fastuidraw_clipper_data_iterator_read(in fastuidraw_clipper_data_iterator iter,
                                      in fastuidraw_clipper_data src)
{
  uint r0, r1, r2;
  uvec4 m0_3, m4_7;

  m0_3 = fastuidraw_geom_mask0_3(iter.count);
  m4_7 = fastuidraw_geom_mask4_7(iter.count);

#define local_helper(A, B) ((A.x & B.x) + (A.y & B.y) + (A.z & B.z) + (A.w & B.w)) 

  r0 = local_helper(src.b0.element0_3, m0_3)
     + local_helper(src.b0.element4_7, m4_7);

  r1 = local_helper(src.b1.element0_3, m0_3)
     + local_helper(src.b1.element4_7, m4_7);

  r2 = local_helper(src.b2.element0_3, m0_3)
     + local_helper(src.b2.element4_7, m4_7);

  return uintBitsToFloat(uvec3(r0, r1, r2));
}

void
fastuidraw_clipper_data_iterator_increment(inout fastuidraw_clipper_data_iterator iter)
{
  ++iter.count;
}

void
fastuidraw_clipper_data_iterator_decrement(inout fastuidraw_clipper_data_iterator iter)
{
  --iter.count;
}

void
fastuidraw_clipper_data_iterator_init_back(out fastuidraw_clipper_data_iterator d,
                                           in fastuidraw_clipper_data src)
{
  d.count = src.count;
  fastuidraw_clipper_data_iterator_decrement(d);
}

void
fastuidraw_clipper_data_field_init(out fastuidraw_clipper_data_field d)
{
  d.element0_3 = uvec4(0u);
  d.element4_7 = uvec4(0u);
}

void
fastuidraw_clipper_data_init(out fastuidraw_clipper_data d)
{
  d.count = 0;
  d.last_barycentric = vec3(0.0);
  fastuidraw_clipper_data_field_init(d.b0);
  fastuidraw_clipper_data_field_init(d.b1);
  fastuidraw_clipper_data_field_init(d.b2);
}

void
fastuidraw_clipper_data_push_back(inout fastuidraw_clipper_data d, in vec3 b)
{
  d.b0.element0_3 += fastuidraw_geom_mask0_3(d.count) & floatBitsToUint(b.x);
  d.b1.element0_3 += fastuidraw_geom_mask0_3(d.count) & floatBitsToUint(b.y);
  d.b2.element0_3 += fastuidraw_geom_mask0_3(d.count) & floatBitsToUint(b.z);

  d.b0.element4_7 += fastuidraw_geom_mask4_7(d.count) & floatBitsToUint(b.x);
  d.b1.element4_7 += fastuidraw_geom_mask4_7(d.count) & floatBitsToUint(b.y);
  d.b2.element4_7 += fastuidraw_geom_mask4_7(d.count) & floatBitsToUint(b.z);

  d.last_barycentric = b;
  ++d.count;
}

void
fastuidraw_clipper_data_pad(inout fastuidraw_clipper_data d, in int N)
{
  while (d.count < N)
    {
      fastuidraw_clipper_data_push_back(d, d.last_barycentric);
    }
}

vec3
fastuidraw_compute_intersection(in vec3 p0, in float c0,
                                in vec3 p1, in float c1)
{
  float t;

  t = c0 / (c0 - c1);
  return mix(p0, p1, t);
}

void
fastuidraw_clip_polygon(in fastuidraw_clipper_data src,
                        in vec3 clip_values,
                        out fastuidraw_clipper_data dst)
{
  vec3 current_bary, last_bary;
  float current_d, last_d;
  bool current_in, last_in;
  fastuidraw_clipper_data_iterator iter;

  last_bary = src.last_barycentric;
  last_d = dot(clip_values, last_bary);
  last_in = bool(last_d >= 0.0);

  fastuidraw_clipper_data_iterator_init_front(iter, src);
  fastuidraw_clipper_data_init(dst);
  
  for (int i = 0; i < src.count; ++i)
    {
      current_bary = fastuidraw_clipper_data_iterator_read(iter, src);
      current_d = dot(clip_values, current_bary);
      current_in = bool(current_d >= 0.0);

      if (current_in != last_in)
        {
          vec3 p;
          p = fastuidraw_compute_intersection(last_bary, last_d,
                                              current_bary, current_d);
          fastuidraw_clipper_data_push_back(dst, p);
        }

      if (current_in)
        {
          fastuidraw_clipper_data_push_back(dst, current_bary);
        }

      last_bary = current_bary;
      last_d = current_d;
      last_in = current_in;
      fastuidraw_clipper_data_iterator_increment(iter);
    }
}

#define fastuidraw_clip_polygon_helper_define(X)                        \
    {                                                                   \
      current_bary = fastuidraw_clipper_data_bary##X(src);              \
      current_d = dot(clip_values, current_bary);                       \
      current_in = bool(current_d >= 0.0);                              \
      if (current_in != last_in)                                        \
        {                                                               \
          vec3 p;                                                       \
          p = fastuidraw_compute_intersection(last_bary, last_d,        \
                                              current_bary, current_d); \
          fastuidraw_clipper_data_push_back(dst, p);                    \
        }                                                               \
                                                                        \
      if (current_in)                                                   \
        {                                                               \
          fastuidraw_clipper_data_push_back(dst, current_bary);         \
        }                                                               \
                                                                        \
      last_bary = current_bary;                                         \
      last_d = current_d;                                               \
      last_in = current_in;                                             \
    }
          

void
fastuidraw_clip_polygon3(in fastuidraw_clipper_data src,
                         in vec3 clip_values,
                         out fastuidraw_clipper_data dst)
{
  vec3 current_bary, last_bary;
  float current_d, last_d;
  bool current_in, last_in;

  last_bary = src.last_barycentric;
  last_d = dot(clip_values, last_bary);
  last_in = bool(last_d >= 0.0);

  fastuidraw_clipper_data_init(dst);
  fastuidraw_clip_polygon_helper_define(0);
  fastuidraw_clip_polygon_helper_define(1);
  fastuidraw_clip_polygon_helper_define(2);
}

void
fastuidraw_clip_polygon4(in fastuidraw_clipper_data src,
                         in vec3 clip_values,
                         out fastuidraw_clipper_data dst)
{
  vec3 current_bary, last_bary;
  float current_d, last_d;
  bool current_in, last_in;

  last_bary = src.last_barycentric;
  last_d = dot(clip_values, last_bary);
  last_in = bool(last_d >= 0.0);

  fastuidraw_clipper_data_init(dst);
  fastuidraw_clip_polygon_helper_define(0);
  fastuidraw_clip_polygon_helper_define(1);
  fastuidraw_clip_polygon_helper_define(2);
  fastuidraw_clip_polygon_helper_define(3);
}

void
fastuidraw_clip_polygon5(in fastuidraw_clipper_data src,
                         in vec3 clip_values,
                         out fastuidraw_clipper_data dst)
{
  vec3 current_bary, last_bary;
  float current_d, last_d;
  bool current_in, last_in;

  last_bary = src.last_barycentric;
  last_d = dot(clip_values, last_bary);
  last_in = bool(last_d >= 0.0);

  fastuidraw_clipper_data_init(dst);
  fastuidraw_clip_polygon_helper_define(0);
  fastuidraw_clip_polygon_helper_define(1);
  fastuidraw_clip_polygon_helper_define(2);
  fastuidraw_clip_polygon_helper_define(3);
  fastuidraw_clip_polygon_helper_define(4);
}

void
fastuidraw_clip_polygon6(in fastuidraw_clipper_data src,
                         in vec3 clip_values,
                         out fastuidraw_clipper_data dst)
{
  vec3 current_bary, last_bary;
  float current_d, last_d;
  bool current_in, last_in;

  last_bary = src.last_barycentric;
  last_d = dot(clip_values, last_bary);
  last_in = bool(last_d >= 0.0);

  fastuidraw_clipper_data_init(dst);
  fastuidraw_clip_polygon_helper_define(0);
  fastuidraw_clip_polygon_helper_define(1);
  fastuidraw_clip_polygon_helper_define(2);
  fastuidraw_clip_polygon_helper_define(3);
  fastuidraw_clip_polygon_helper_define(4);
  fastuidraw_clip_polygon_helper_define(5);
}

void
fastuidraw_clipper_data_emit(in fastuidraw_clipper_data src)
{
  fastuidraw_clipper_data_iterator forward, backward;
  bool take_forward = false;

  if (src.count >= 3)
    {
      fastuidraw_clipper_data_iterator_init_front(forward, src);
      fastuidraw_clipper_data_iterator_init_back(backward, src);

      fastuidraw_emit_vertex(fastuidraw_clipper_data_iterator_read(forward, src));
      fastuidraw_clipper_data_iterator_increment(forward);

      while (forward.count <= backward.count)
        {
          if (take_forward)
            {
              fastuidraw_emit_vertex(fastuidraw_clipper_data_iterator_read(forward, src));
              fastuidraw_clipper_data_iterator_increment(forward);
            }
          else
            {
              fastuidraw_emit_vertex(fastuidraw_clipper_data_iterator_read(backward, src));
              fastuidraw_clipper_data_iterator_decrement(backward);
            }
          take_forward = !take_forward;
        }
      EndPrimitive();
    }
}

void
fastuidraw_clipper_data_emit_full(in fastuidraw_clipper_data src)
{
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary0(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary6(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary1(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary5(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary2(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary4(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary3(src));
  EndPrimitive();
}

void
fastuidraw_clipper_data_emit_tris_full(in fastuidraw_clipper_data src)
{
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary0(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary1(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary2(src));
  EndPrimitive();

  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary0(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary2(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary3(src));
  EndPrimitive();

  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary0(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary3(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary4(src));
  EndPrimitive();

  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary0(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary4(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary5(src));
  EndPrimitive();

  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary0(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary5(src));
  fastuidraw_emit_vertex(fastuidraw_clipper_data_bary6(src));
  EndPrimitive();
}

void
main(void)
{
  fastuidraw_clipper_data A0, A1, A2, A3, A4;
  vec3 C;
  
  fastuidraw_clipper_data_init(A0);
  fastuidraw_clipper_data_push_back(A0, vec3(1.0, 0.0, 0.0));
  fastuidraw_clipper_data_push_back(A0, vec3(0.0, 1.0, 0.0));
  fastuidraw_clipper_data_push_back(A0, vec3(0.0, 0.0, 1.0));

  C.x = fastuidraw_in[0].fastuidraw_clip_planes.x;
  C.y = fastuidraw_in[1].fastuidraw_clip_planes.x;
  C.z = fastuidraw_in[2].fastuidraw_clip_planes.x;
  fastuidraw_clip_polygon3(A0, C, A1);
  fastuidraw_clipper_data_pad(A1, 4);

  C.x = fastuidraw_in[0].fastuidraw_clip_planes.y;
  C.y = fastuidraw_in[1].fastuidraw_clip_planes.y;
  C.z = fastuidraw_in[2].fastuidraw_clip_planes.y;
  fastuidraw_clip_polygon4(A1, C, A2);
  fastuidraw_clipper_data_pad(A2, 5);

  C.x = fastuidraw_in[0].fastuidraw_clip_planes.z;
  C.y = fastuidraw_in[1].fastuidraw_clip_planes.z;
  C.z = fastuidraw_in[2].fastuidraw_clip_planes.z;
  fastuidraw_clip_polygon5(A2, C, A3);
  fastuidraw_clipper_data_pad(A3, 6);

  C.x = fastuidraw_in[0].fastuidraw_clip_planes.w;
  C.y = fastuidraw_in[1].fastuidraw_clip_planes.w;
  C.z = fastuidraw_in[2].fastuidraw_clip_planes.w;
  fastuidraw_clip_polygon6(A3, C, A4);
  fastuidraw_clipper_data_pad(A4, 7);

  fastuidraw_clipper_data_emit(A2);
}

#else

layout(triangle_strip, max_vertices = 3) out;
void
main(void)
{
  fastuidraw_emit_vertex(vec3(1.0, 0.0, 0.0));
  fastuidraw_emit_vertex(vec3(0.0, 1.0, 0.0));
  fastuidraw_emit_vertex(vec3(0.0, 0.0, 1.0));
}

#endif
